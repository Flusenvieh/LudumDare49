title: Start_Gluttony
tags: 
colorID: 0
position: 320,-400
---
<<if $did_meet_gluttony is false>>
    <<set $did_meet_gluttony to true>>
    Orange: Hey! Welcome back!
    Player: ...
    Player: What are you doing here?
    Orange: I'm cooking. Well, I wanted to cook at least.
    Orange: But the pantry is empty, there is no food.
    Player: Did you eat it?
    Orange: Well, maybeee...
    Orange: But that doesn't matter now!
    Orange: Could you do me a favour and fetch me some ingredients?
    -> Why not.
        [[Gluttony_AcceptQuest]]
    -> Meh, do it yourself!
        [[Gluttony_RejectQuest]]
<<else>>
    <<if $gluttony_quest_complete is false>>
        Orange: Do you have my ingredients?
        -> Yes, here!<<if visited("Item_Pickup_Groceries") is true>>
            [[Gluttony_CompleteQuest]]
        -> No, not yet
            Orange: Then off you go
    <<else>>
        Orange: I'm sure you have better stuff to do than helping me.
    <<endif>>
<<endif>>
===
title: Gluttony_AcceptQuest
tags: 
colorID: 0
position: 920,80
---
Player: But I better get a portion of whatever you're making when you're done!
Orange: I would say that you can count on me, but food seems to disappear around me...
Orange: But I promise to help you handle the others.
Player: OTHERS???
Orange: You will see.
Orange: Now get me those ingredients!
<<nextStage>>
===
title: Gluttony_RejectQuest
tags: 
colorID: 0
position: 920,-240
---
Orange: Please!
[[Nope!|Gluttony_RejectQuest]]
[[Okay, okay, I'll do it!|Gluttony_AcceptQuest]]
===
title: Gluttony_CompleteQuest
tags: 
colorID: 0
position: 269,58
---
<<set $gluttony_quest_complete to true>>
Orange: Yaaaay, you found something. Lemme see!
Orange: ... Salad, ugh...
Orange: ... Cheese, yum ...
Orange: ... And I can use that stuff too...
Orange: Okay, I think I can come up with something delicious now!
Orange: You know where to find me when you need something.
<<nextStage>>
===
title: Start_Wrath
tags: 
colorID: 0
position: 163,520
---
<<if visited("Start_Wrath") is false>>
    Red: GRRROOOAAHHH!
    Red: *Shows bar, but the adjustable weights seem missing*
    Player: Do you want me to find the missing weights?
    Red: UHUH! *nods head*
<<endif>>
<<if $weight_item > 0 >>
    Player: Now you can continue your training
    Red: RAAAH! *puts weights on bar*
    <<if $weight_item < 3>>
        Red: HURGH! *lifts bar without struggling*
        Player: Maybe you need something heavier...
    <<elseif $weight_item eq 3>>
        Red: HUUUURGHHHH! *lifts bar slowly*
        Player: Seems perfect
        <<nextStage>>
    <<else>>
        Red: UMMMPFFF! *weight doesn't move*
        Player: Perhaps a bit too much...
    <<endif>>
<<endif>>
===
title: Start_Greed
tags: 
colorID: 0
position: 400,520
---
<<if $did_meet_greed is false>>
    <<set $did_meet_greed to true>>
    Yellow: What do you want?
    -> Nothing
        Yellow: Then leave me alone.
    -> Say hello
        Yellow: Hello back.
        Yellow: Now go away.
    Yellow: And before you ask, you also can't have those weights, those are mine now.
<<elseif $has_gold is false and $weight_item eq 0>>
    Yellow: Go away
<<endif>>
<<if $has_gold is true>>
    Yellow: What's that shiny thing?
        -> Gold
        -> Something valuable
    Yellow: Okay, I would exchange it for one of those weights.
    Yellow: Which one do you want?
    <<set $weight_item to 0>>
    -> 98kg
        <<set $weight_item to 1>>
    -> 152kg
        <<set $weight_item to 2>>        
    -> 294kg
        <<set $weight_item to 3>>        
    -> 366kg
        <<set $weight_item to 4>>
    <<set $has_gold to false>>
<<elseif $weight_item > 0 >>
    Player: Can I please exchange my weight?
    Yellow: Hmmm... Which one do you want?
    -> 98kg <<if $weight_item neq 1 >>
        <<set $weight_item to 1>>
    -> 152kg <<if $weight_item neq 2 >>
        <<set $weight_item to 2>>        
    -> 294kg <<if $weight_item neq 3 >>
        <<set $weight_item to 3>>        
    -> 366kg <<if $weight_item neq 4 >>
        <<set $weight_item to 4>>
<<endif>>
===
title: Gold_Gluttony
tags: 
colorID: 0
position: 680,520
---
Orange: Hey
<<if visited("Start_Wrath") is true and $has_gold is false and $weight_item eq 0>>
    Orange: Look what I found!
    Orange: I tried to eat it, but I hurt my teeth instead.
    Orange: Here, take it!
    <<set $has_gold to true>>
<<endif>>
===
title: Start_RoomsQuest
tags: 
colorID: 0
position: 80,920
---
Cyan: Heeey, someone wants to sleep here!
Cyan: Stop making such weird noises!
Pink: Stop being such a bore!
Cyan: Bore? You're a weirdo!
Pink: The only weird one here would sleep 25h a day if it was possible.
Cyan: Says the one with questionable objects in their nightstand drawer.
Pink: At least those are fun, in contrast to a certain roommate.
Cyan: Fun? I dunno how...
Cyan: Oh no, not another one...
-> Who, me?
-> Sorry, I didn't mean to intrude...
Pink: Oh, it's okay. I like company - especially interesting people...
Cyan: I HEARD THAT! Yeesh, take a room, you two...
Cyan: *dozes off*
<<nextStage>>
===
title: RoomQuest_Sloth
tags: 
colorID: 0
position: -200,1000
---
<<if visited("Start_RoomsQuest") is false>>
    [[Start_RoomsQuest]]
<<else>>
    <<if $placement_pickup_counter < 7>>
        Cyan: ZZZZZzzzz
    <<else>>
        <<if visited("End_RoomsQuest") is false>>
            [[End_RoomsQuest]]
        <<else>>
            Cyan: 5 more minutes...
        <<endif>>
    <<endif>>
<<endif>>
===
title: RoomQuest_Lust
tags: 
colorID: 0
position: 360,1000
---
<<if visited("Start_RoomsQuest") is false>>
    [[Start_RoomsQuest]]
<<else>>
    <<if $placement_pickup_counter < 7>>
        Pink: Pssst, be quiet! I don't want to deal with a cranky sleepyhead
    <<else>>
        <<if visited("End_RoomsQuest") is false>>
            [[End_RoomsQuest]]
        <<else>>
            Pink: Come on, wake up!
        <<endif>>
    <<endif>>
<<endif>>
===
title: Item_Pickup_Gluttony
tags: 
colorID: 0
position: -640,1360
---
Chest: *This chest contains food magazines and cookbooks*
Chest: *You put everything in your inventory*
<<set $room_item to 1>>
<<if visited("Item_Pickup_Gluttony") is false>>
    <<set $moving_opened_counter to $moving_opened_counter + 1>>
<<endif>>
===
title: Item_Pickup_Greed
tags: 
colorID: 0
position: -400,1360
---
Chest: *From glittery shiny things to literal rocks, this chest contains a heap of collected things*
Chest: *You take out a bag and put everything in it*
Chest: *Hopefully it won't break and spill everything on the floor*
<<set $room_item to 2>>
<<if visited("Item_Pickup_Greed") is false>>
    <<set $moving_opened_counter to $moving_opened_counter + 1>>
<<endif>>
===
title: Item_Pickup_Pride
tags: 
colorID: 0
position: -160,1360
---
Chest: *This chest's owner seems to love taking selfies*
Chest: *You pick up a big photo album*
<<set $room_item to 3>>
<<if visited("Item_Pickup_Pride") is false>>
    <<set $moving_opened_counter to $moving_opened_counter + 1>>
<<endif>>
===
title: Item_Pickup_Sloth
tags: 
colorID: 0
position: 80,1360
---
Chest: *A plushy zoo awaits you after opening this chest*
Chest: *You try to guide them to their new home*
<<set $room_item to 4>>
<<if visited("Item_Pickup_Sloth") is false>>
    <<set $moving_opened_counter to $moving_opened_counter + 1>>
<<endif>>
===
title: Item_Pickup_Envy
tags: 
colorID: 0
position: 320,1360
---
Chest: *Weird, this chest only contains a list of things*
<<if $moving_opened_counter > 0 and $moving_opened_counter < 7 >>
    Chest: *Even weirder, you have seen some of those things recently*
<<endif>>
<<if $moving_opened_counter eq 6 >>
    Chest: *Hey, those are all things from the other chests!*
<<endif>>
Chest: *Still, you put it in your bag*
<<set $room_item to 5>>
===
title: Item_Pickup_Wrath
tags: 
colorID: 0
position: 560,1360
---
Chest: *Fitness seems to be the main concern of this chest's owner*
Chest: *Moving this stuff is going to be challenging*
<<set $room_item to 6>>
<<if visited("Item_Pickup_Wrath") is false>>
    <<set $moving_opened_counter to $moving_opened_counter + 1>>
<<endif>>
===
title: Item_Pickup_Lust
tags: 
colorID: 0
position: 800,1360
---
Chest: *You don't know what half of this stuff is, and you get the slight feeling that it's better this way*
Chest: *Carefully you pick everything up, trying to touch everything as little as possible*

<<set $room_item to 7>>
<<if visited("Item_Pickup_Lust") is false>>
    <<set $moving_opened_counter to $moving_opened_counter + 1>>
<<endif>>
===
title: Item_Placement_Gluttony
tags: 
colorID: 0
position: -640,1680
---
<<if $room_item eq 1>>
    Chest: *You put the books away*
    <<set $room_item to 0>>
    <<destroy GluttonyChestOld true>>
    <<destroy GluttonyChestOverlay true>>
    <<destroy Chests_2 true>>
    <<set $placement_pickup_counter to $placement_pickup_counter + 1>>
<<else>>
    Player: Nah, that can't be right
<<endif>>
===
title: Item_Placement_Greed
tags: 
colorID: 0
position: -400,1680
---
<<if $room_item eq 2>>
    Chest: *You try to organize this heap of things, but in the end you just put it all into a big box*
    <<set $room_item to 0>>
    <<destroy GreedChestOld true>>
    <<destroy GreedChestOverlay true>>
    <<destroy Chests_6 true>>
    <<set $placement_pickup_counter to $placement_pickup_counter + 1>>
<<else>>
    Player: Nah, that can't be right
<<endif>>
===
title: Item_Placement_Pride
tags: 
colorID: 0
position: -160,1680
---
<<if $room_item eq 3>>
    Chest: You put the selfie album on the nightstand.
    <<set $room_item to 0>>
    <<destroy PrideChestOld true>>
    <<destroy PrideChestOverlay true>>
    <<destroy Chests_3 true>>
    <<set $placement_pickup_counter to $placement_pickup_counter + 1>>
<<else>>
    Player: Nah, that can't be right
<<endif>>
===
title: Item_Placement_Sloth
tags: 
colorID: 0
position: 80,1680
---
<<if $room_item eq 4>>
    Chest: *You take your time with placing every plushie on the bed, and hug each of them*
    <<set $room_item to 0>>
    <<destroy SlothChestOld true>>
    <<destroy SlothChestOverlay true>>
    <<destroy Chests_4 true>>
    <<set $placement_pickup_counter to $placement_pickup_counter + 1>>
<<else>>
    Player: Nah, that can't be right
<<endif>>
===
title: Item_Placement_Envy
tags: 
colorID: 0
position: 320,1680
---
<<if $room_item eq 5>>
    Chest: *You take a last skeptical look at the list of things and leave it in the room*
    <<set $room_item to 0>>
    <<destroy EnvyChestOld true>>
    <<destroy EnvyChestOverlay true>>
    <<destroy Chests_1 true>>
    <<set $placement_pickup_counter to $placement_pickup_counter + 1>>
<<else>>
    Player: Nah, that can't be right
<<endif>>
===
title: Item_Placement_Wrath
tags: 
colorID: 0
position: 560,1680
---
<<if $room_item eq 6>>
    Chest: *On the last few steps your strength leaves you*
    Chest: *You find out that heavy fitness equipment hurts when it lands on your foot*
    <<set $room_item to 0>>
    <<destroy WrathChestOld true>>
    <<destroy WrathChestOverlay true>>
    <<destroy Chests_0 true>>
    <<set $placement_pickup_counter to $placement_pickup_counter + 1>>
<<else>>
    Player: Nah, that can't be right
<<endif>>
===
title: Item_Placement_Lust
tags: 
colorID: 0
position: 800,1680
---
<<if $room_item eq 7>>
    Chest: *You just dump everything into a drawer and try to forget about its contents*
    <<set $room_item to 0>>
    <<destroy LustChestOld true>>
    <<destroy LustChestOverlay true>>
    <<destroy Chests_5 true>>
    <<set $placement_pickup_counter to $placement_pickup_counter + 1>>
<<else>>
    Player: Nah, that can't be right
<<endif>>
===
title: Item_Pickup_Groceries
tags: 
colorID: 0
position: -240,-280
---
Bag: *Seems like someone bought groceries*
Bag: *You pick the bag up*
<<destroy Groceries true>>
===
title: End_RoomsQuest
tags: 
colorID: 0
position: -80,2480
---
<<if visited("End_RoomsQuest") is false>>
    Player: Hey, I found some spare rooms. Now you don't have to share.
    Pink: Nice. I hope the walls are quite soundproof.
    Cyan: *slowly wakes up* Five more minutes...
    Pink: No, except you want to stay in this room together.
    Pink: Come, let's look for our new rooms.
    Cyan: Okay, I'm coming. I just have to wake up a bit...
    Pink: That could take hours. Maybe go on without us.
    <<nextStage>>
<<endif>>
===
title: Start_Envy_Pride
tags: 
colorID: 0
position: -80,2840
---
Blue: I told you: This is my room and I won't give it to you!
Green: But it is not fair. Your room is way bigger than all the other rooms.
Blue: If you want a big room you can stay in our previously shared room.
Green: You know that I can't do that. That is not allowed anymore.
Blue: Well then: Bad luck!
Green: Oh come on! You have so many cool things. Can't you give up at least one thing?
Blue: Guess not, sorry.
Green: Hmmm... Oh hey, it's you. Can you please help?
-> I can try.
===
title: TrainingPlan
tags: 
colorID: 0
position: 920,520
---
*Some notes about training progress*
Monday:     3kg
Tuesday:    4kg
Wednesday:  8kg
Thursday:   11kg
Friday:     44kg
Saturday:   49kg
Sunday:     ???
===
title: End_Envy_Pride
tags: 
colorID: 0
position: -80,3120
---
Blue: Hey! Where is my incredible medal collection?
Green: I think I have seen it in another room.
Blue: Did you put it there?
Green: Maybe. Does it bother you?
Blue: No... not at all... buuuut I just realized something.
Green: What?
Blue: I think I don't really like this room anymore. It actually looks a bit boring. You can have it.
Green: Oh! Really? Thanks! But now that you mention it, you are actually kind of right...
Blue: Too late. It is yours now.

<<wait 3>>
<<nextStage>>
[[End]]
===
title: Envy
tags: 
colorID: 0
position: 240,2960
---
<<if visited("Start_Envy_Pride") is false>>
    [[Start_Envy_Pride]]
<<else>>
    <<if $finished_Medal_Puzzle is false>>
        Green: Please hurry! I need this room.
    <<else>>
        <<if visited("End_Envy_Pride") is false>>
            [[End_Envy_Pride]]
        <<endif>>
    <<endif>>
<<endif>>
===
title: Pride
tags: 
colorID: 0
position: -320,2960
---
<<if visited("Start_Envy_Pride") is false>>
    [[Start_Envy_Pride]]
<<else>>
    <<if $finished_Medal_Puzzle is false>>
        Blue: Are you also here to bother me? This is my room and I won't give it to you.
        Blue: I mean, have you seen how nice my remarkable medal collection looks in here?
    <<else>>
        <<if visited("End_Envy_Pride") is false>>
            [[End_Envy_Pride]]
        <<endif>>
    <<endif>>
<<endif>>
===
title: End
tags: 
colorID: 0
position: -80,3520
---
Orange: I like him. Without him I probably would have starved.
Yellow: I doubt that. But yeah, he has a good feeling about what we want and need.
Red: Groaaaaaaahhhh!!! *Happy grunt noices*
Pink: That is exactly what I wanted to say, big guy. He has a good feeling about privacy.
Cyan: ZZZZZZZZZZzzzzzzzz
Blue: He did a good job at placing my magnificent medal collection.
Green: This room really looks damn boring... but it was really nice of him to help me.
Player: ...
Therapist: Well then. Our session for today is over. I think we made big advancements this time.
Therapist: Maybe your brain isn't as "unstable" as you always label it.
<<wait 3>>
<<nextStage>>
===
title: RoomsNote
tags: 
colorID: 0
position: 1100,1460
---
1. Blue doesn't want to live opposite of someone
2. Cyan just wants to sleep in peace, the exact position doesn't matter
3. Orange wants to live next to Blue
4. Blue and Pink don't like each other and prefer to keep a distance between each other
5. Yellow wants the first room on the right side
6. Green dislikes Pink, they are definitely not made to be direct neighbours
7. Green wants to live next to Orange and Red
8. Red only cares about training
===
title: OldMedal
tags: 
colorID: 0
position: 520,2880
---
<<if visited("Start_Envy_Pride") is true>>
    Player: Maybe I could move this to another room...
    Player: *You take the medal collection with you*
    <<destroy MedalOld true>>
    <<set $enable_medal_puzzle to true>>
<<endif>>
===
title: NewMedal
tags: 
colorID: 0
position: 800,2880
---
<<if $enable_medal_puzzle is true>>
    <<enable 0>>
<<endif>>
===
title: MedalGameFinish
tags: 
colorID: 0
position: 1120,2840
---
<<enable 1>>
<<set $finished_Medal_Puzzle to true>>
<<destroy MedalGame true>>
<<destroy MedalNew false>>
===
